buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net/' }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
	}
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${mc_version}-${lib_version}"
group= "com.cd4017be.lib"
archivesBaseName = "cd4017be_lib"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
	mappings channel: mc_mapping_channel, version: mc_mapping_version

	accessTransformer = file('src/main/resources/META-INF/cd4017be_lib_at.cfg')

	runs {
		client {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'
		}

		server {
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'
		}
	}
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'deobf'
}

tasks.build.dependsOn('deobfJar')

artifacts {
	archives deobfJar
}

repositories {
	maven {
		// location of the maven that hosts JEI files
		name = "Progwml6 maven"
		url = "http://dvs1.progwml6.com/files/maven/"
	}
	maven {
		// location of a maven mirror for JEI files, as a fallback
		name = "ModMaven"
		url = "modmaven.k-4u.nl"
	}
	maven {
		url = "http://maven.cil.li/"
	}
	maven {
		url = "http://maven.ic2.player.to/"
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

	// JEI
	compileOnly "mezz.jei:jei_${mc_version}:${jei_version}:api"
	runtimeOnly fg.deobf("mezz.jei:jei_${mc_version}:${jei_version}")
	// OC
	compileOnly "li.cil.oc:OpenComputers:${oc_version}"
	// TIS-3D
	compileOnly "li.cil.tis3d:TIS-3D:${tis3d_version}"
	// IC2
	compileOnly "net.industrial-craft:industrialcraft-2:${ic2_version}:api"
}

jar {
	manifest {
		attributes([
				"Specification-Title": "cd4017be_lib",
				"Specification-Vendor": "cd4017be",
				"Specification-Version": "1",
				"Implementation-Title": project.name,
				"Implementation-Version": "${version}",
				"Implementation-Vendor" :"cd4017be",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

tasks.publish.dependsOn('reobfJar', 'deobfJar')
publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/cd4017be/cd4017be_lib")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			artifactId archivesBaseName
			artifact jar
			artifact deobfJar
		}
	}
}
